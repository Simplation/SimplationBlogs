## Gradle Catalog

### 引言
随着项目的不断迭代和升级，依赖的版本会逐渐变大，使用统一的依赖库管理就显得非常有必要。
已有的统一依赖库管理的方式：

- 使用循环优化 `Gradle` 依赖管理
- 使用 `buildSrc` 管理 `Gradle` 依赖
- 使用 `includeBuild` 统一配置依赖版本

已有的方式支持在不同 `Module` 中共享，如果需要项目间进行共享，发布到远端即可实现共享的功能，基本上实现相应的功能。这些都不是重点，
主要是说一下 `Gradle 7.0` 版本推出的新特性，使用 Catalog 进行统一依赖版本的管理，具备新特性：

- 对所有 module 可见，可统一管理所有 module 的依赖
- 支持声明依赖 bundles,即总是一起使用的依赖可以组合在一起
- 支持版本号与依赖名分离，可以在多个依赖间共享版本号
- 支持在单独的 libs.versions.toml 文件中配置依赖
- 支持在项目间共享依赖

### 使用 Version Catalog

- 启用

  `Catalog` 当前还是预览功能，需要使用以下代码来启用

  ```groovy
  enableFeaturePreview("VERSION_CATALOGS")
  ```

- 配置

  Version Catalog 其实就是一个版本目录，从版本目录中选出需要的依赖，使用下面的方式进行配置。

  ```groovy
  dependencies {
  	implementation(libs.retrofit)
  	implementation(libs.groovy.core)
  }
  ```

- Version Catalog 优点

  - 对于每个 catalog，`Gradle` 都会生成类型安全的访问器，以便你在 `IDE` 中可以自动补全.(注:目前在 `build.gradle` 中还不能自动补全，可能是指 `kts` 或者开发中？)
  - 声明在 catalog 中的依赖对所有 module 可见，当修改版本号时，可以统一管理统一修改
  - catalog 支持声明一个依赖 bundles，即一些总是一起使用的依赖的组合
  - catalog 支持版本号与依赖名分离，可以在多个依赖间共享版本号

### 声明 Version Catalog

`Version Catalog` 可以在 setting.gradle(.kts) 文件中声明。

```groovy
dependencyResolutionManagement {
	versionCatalogs {
		libs {
			alias('retrofit').to('com.squareup.retrofit2:retrofit:2.9.0')
			alias('groovy-core').to('org.codehaus.groovy:groovy:3.0.5')
			alias('groovy-json').to('org.codehaus.groovy:groovy-json:3.0.5')
			alias('groovy-nio').to('org.codehaus.groovy:groovy-nio:3.0.5')
			alias('commons-lang3').to('org.apache.commons', 'commons-lang3').version {
				strictly '[3.8, 4.0['
				prefer '3.9'
			}
		}
	}
}
```

> 别名必须由一系列以破折号（-，推荐）、下划线 (_) 或点 (.) 分隔的标识符组成。 标识符本身必须由 ascii 字符组成，最好是小写，最后是数字。
>
> 🎯 注：groovy-core 会被映射成 libs.groovy.core，如果你想避免映射可以使用大小写来区分，比如 groovyCore 会被处理成 libs.groovyCore

如果出现具有相同版本号的依赖，可以使用统一的方式进行处理；

```groovy
dependencyResolutionManagement {
	versionCatalogs {
        	libs {
			version('groovy', '3.0.5')
			version('compilesdk', '30')
			version('targetsdk', '30')
			alias('groovy-core').to('org.codehaus.groovy', 'groovy').versionRef('groovy')
                        alias('groovy-json').to('org.codehaus.groovy', 'groovy-json').versionRef('groovy')
                        alias('groovy-nio').to('org.codehaus.groovy', 'groovy-nio').versionRef('groovy')
			alias('commons-lang3').to('org.apache.commons', 'commons-lang3').version {
				strictly '[3.8, 4.0['
				prefer '3.9'
			}
		}
	}
}
```

当然，也可以在 `build.gradle` 文件中指定 `compilesdk` 和 `targetsdk`

```groovy
android {
	compileSdk libs.versions.compilesdk.get().toInteger()

	defaultConfig {
        	applicationId "com.simplation.gradlecatalog"
        	minSdk 21
        	targetSdk libs.versions.targetsdk.get().toInteger()
        	versionCode 1
        	versionName "1.0"

        	testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}
}
```

### 依赖 `bundles`
为了方便管理项目中一起使用的依赖项，`Catalog` 引入 `bundles`(依赖包) 的方式。依赖包基本上是几个依赖项打包的别名。 例如，你可以这样使用一个依赖包，而不是像上面那样声明 3 个单独的依赖项：

```groovy
dependencies {
	implementation libs.bundles.groovy
}
```

`groovy` 依赖包的声明如下：

```groovy
dependencyResolutionManagement {
	versionCatalogs {
        	libs {
			version('groovy', '3.0.5')
			version('checkstyle', '8.37')
			alias('groovy-core').to('org.codehaus.groovy', 'groovy').versionRef('groovy')
			alias('groovy-json').to('org.codehaus.groovy', 'groovy-json').versionRef('groovy')
			alias('groovy-nio').to('org.codehaus.groovy', 'groovy-nio').versionRef('groovy')
			alias('commons-lang3').to('org.apache.commons', 'commons-lang3').version {
				strictly '[3.8, 4.0['
				prefer '3.9'
			}
            
			// 使用 `bundles`
			bundle('groovy', ['groovy-core', 'groovy-json', 'groovy-nio'])  // ←
		}
	}
}
```

### 插件版本

除了 `Library` 之外，`Catalog` 还支持声明插件版本。 因为 `library` 由它们的 group、artifact 和 version 表示，但 `Gradle` 插件仅由它们的 id 和 version 标识。 因此，插件需要单独声明：

```groovy
dependencyResolutionManagement {
        versionCatalogs {
		libs {
			alias('jmh').toPluginId('me.champeau.jmh').version('0.6.5')
        	}
	}
}
```

然后可以在 `plugins` 块下面使用

```groovy
plugins {
	id 'java-library'
        id 'checkstyle'
        // 使用声明的插件
        alias(libs.plugins.jmh)
}
```

### 使用单独文件配置 `Catalog`
除了在 `settings.gradle` 中声明 Catalog 外，也可以通过一个单独的文件来配置 Catalog 如果在根构建的 `gradle` 目录中找到了 `libs.versions.toml` 文件,则将使用该文件的内容自动声明一个 Catalog

`TOML` 文件主要由 4 个部分组成：

- [versions] 用于声明可以被依赖项引用的版本

- [libraries] 用于声明 Library 的别名

- [bundles] 用于声明依赖包

- [plugins] 用于声明插件

  ```toml
  # Samples
  [versions]
  groovy = "3.0.5"
  checkstyle = "8.37"
  compilesdk = "30"
  targetsdk = "30"
  
  [libraries]
  retrofit = "com.squareup.retrofit2:retrofit:2.9.0"
  groovy-core = { module = "org.codehaus.groovy:groovy", version.ref = "groovy" }
  groovy-json = { module = "org.codehaus.groovy:groovy-json", version.ref = "groovy" }
  groovy-nio = { module = "org.codehaus.groovy:groovy-nio", version.ref = "groovy" }
  commons-lang3 = { group = "org.apache.commons", name = "commons-lang3", version = { strictly = "[3.8, 4.0[", prefer="3.9" } }
  
  [bundles]
  groovy = ["groovy-core", "groovy-json", "groovy-nio"]
  
  [plugins]
  jmh = { id = "me.champeau.jmh", version = "0.6.5" }
  ```

  如上所示，依赖可以定义成一个字符串，也可以将 module 与 version 分离开来 其中 versions 可以定义成一个字符串，也可以定义成一个范围，详情可参见 rich-version

  ```toml
  [versions]
  my-lib = { strictly = "[1.0, 2.0[", prefer = "1.2" }
  ```

### 发布插件以实现项目间共享

- 通过文件共享

  - Catalog 支持通过从 `Toml` 文件引入依赖，这就让我们可以通过指定文件路径来实现共享依赖 如下所示，我们在 `settins.gradle` 中配置如下：

    ```groovy
    dependencyResolutionManagement {
    	versionCatalogs {
    		libs {
    			from(files("../gradle/libs.versions.toml"))
    		}
    	}
    }
    ```

  - 此技术可用于声明来自不同文件的多个目录：

    ```groovy
    dependencyResolutionManagement {
    	versionCatalogs {
    		// 声明一个 'testLibs' 目录, 从 'test-libs.versions.toml' 文件中
    		testLibs {
    			from(files('gradle/test-libs.versions.toml'))
    		}
    	}
    }
    ```

- 通过发布插件实现共享

  虽然从本地文件导入 Catalog 方便，但它并没有解决在组织或外部消费者中共享 Catalog 的问题。 我们还可能通过 Catalog 插件来发布目录，这样用户直接引入这个插件即可

  `Gradle` 提供了一个 Catalog 插件，它提供了声明然后发布 Catalog 的能力。

  - 首先引入两个插件

    ```groovy
    plugins {
    	id 'com.android.application'
            id 'kotlin-android'
        
    	// 引入插件
    	id 'version-catalog'
    	id 'maven-publish'
    }
    ```

    然后，此插件将公开可用于声明目录的 catalog 扩展

  - 定义目录

    ```groovy
    catalog {
    	// 在此块中声明别名、包和版本
            versionCatalog {
    		from files("../libs.versions.toml")
    	}
    }
    ```
    通过定义目录，然后通过 `maven-publish` 发布

  - 发布目录

    ```groovy
    publishing {
    	publications {
    		maven(MavenPublication) {
                    groupId = 'com.simplation.catalog'
                    artifactId = 'catalog'
                    version = '1.0.0'
                    from components.versionCatalog
    		}
    	}
    }
    ```

    定义好 `groupId`，`artifactId`，`version`，`from` 就可以发布到 `mavenLocal`，也可以根据需要配置发布到自己的 `maven` 以上发布的所有代码可见：[Catalog 发布相关代码](https://github.com/shenzhen2017/GradleCatalog)
    
  - 使用
  
    由于已经发布到 `mavenLocal`，直接引入 `mavenLocal` 就可以使用插件
  
    ```groovy
    # settings.gradle
    dependencyResolutionManagement {
    	//...
    	repositories {
    		mavenLocal()
    		//...
    	}
    }
    
    enableFeaturePreview('VERSION_CATALOGS')
    dependencyResolutionManagement {
    	versionCatalogs {
            	libs {
    			from("com.zj.catalog:catalog:1.0.0")
    			//  也可以重写覆盖 catalog 中的 groovy 版本
    			version("groovy", "3.0.6")
    		}
    	}
    }
    ```

### 参考资料

[Sharing dependency versions between projects (gradle.org)](https://docs.gradle.org/current/userguide/platforms.html)

